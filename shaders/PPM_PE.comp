#version 450

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
//---------------------------------------------------------
// Structs
//---------------------------------------------------------

struct Ray
{
    vec3 pos;
    vec3 dir;
};

struct Photon
{
	vec4 position;
	vec4 power;
	float phi;
	float theta;
    float __padding[2];
};

//---------------------------------------------------------
// Constants
//---------------------------------------------------------
const vec4 SUNLIGHT_COLOR = vec4(1.0f);
const float PI = 3.14159265359;
const float INV_4Pi = 1.0f/(4.0f * PI);
const float PI4_3 = PI * 4.0f / 3.0f;
const float FLT_MAX = 3.402823466e+38;
const float FLT_MIN = 1.175494351e-38;
const vec4 BG_COLORS[5] = 
    {
        vec4(0.00f, 0.0f, 0.02f, 1.0f), // GROUND DARKER BLUE
        vec4(0.01f, 0.05f, 0.2f, 1.0f), // HORIZON GROUND DARK BLUE
        vec4(0.7f, 0.9f, 1.0f, 1.0f), // HORIZON SKY WHITE
        vec4(0.1f, 0.3f, 1.0f, 1.0f),  // SKY LIGHT BLUE
        vec4(0.01f, 0.1f, 0.7f, 1.0f)  // SKY BLUE
    };
const float BG_DISTS[5] = 
{
    -1.0f,
    -0.04f,
    0.0f,
    0.5f,
    1.0f
};

//---------------------------------------------------------
// Descriptor Set
//---------------------------------------------------------
layout (binding = 0, rgba32f) uniform image2D resultImage;
layout (binding = 1) uniform CameraProperties
{
	vec3 position;
	int halfWidth;
	vec3 forward;
    int halfHeight;
    vec3 right;
    float nearPlane;
    vec3 up;
    float pixelSizeY;
    float pixelSizeX;

} cameraProperties;

layout (binding = 2, std430) buffer PhotonMap
{
    Photon photons[];
};
layout (binding = 3) uniform CloudProperties
{
    vec4 bounds[2];
    uvec4 voxelCount;
    float maxExtinction;
    float baseScaling;
    float densityScaling;

} cloudProperties;

layout (binding = 4) uniform Parameters
{
    uint maxRayBounces;
    float lightIntensity;
    float phaseG;  // in [-1, 1]
    float phaseOnePlusG2;
    float phaseOneMinusG2;
    float phaseOneOver2G;
    bool isotropic;

} parameters;

layout (binding = 5) uniform PhotonMapProperties
{
	vec4 lightDirection;
	vec4 bounds[2];
	uvec3 cellCount;
	float cellSize;
	uint photonSize;
    float stepSize;
    float sampleRadius;
	float absorption;

} photonMapProperties;

layout (binding = 6, std430) buffer CollisionMap
{
    uvec4 collisions[];
};

layout (binding = 7) uniform sampler3D cloudSampler;

layout (binding = 8) uniform sampler3D shadowVolumeSampler;

layout (binding = 9) uniform ShadowVolumeProperties
{
    vec4 bounds[2];
	vec4 lightDirection;
    vec4 right;
    vec4 up;
    mat4 basisChange;
    uint voxelAxisCount;
    float voxelSize;

} shadowVolumeProperties;

layout (push_constant) uniform PushConstants
{
    double time;
    int seed;
    uint frameCount;
} pushConstants;

//---------------------------------------------------------
// Helper Functions
//---------------------------------------------------------
vec4 sampleBackground(in vec3 dir)
{
    float dist = dir.y;
    lowp vec4 col = BG_COLORS[0];
    for (int i=1; i < 5; ++i)
    {
        col = mix(col, BG_COLORS[i], smoothstep(BG_DISTS[i-1], BG_DISTS[i], dist));
    }
    return col;
}


float sampleCloud(in vec3 pos)
{
    vec3 normalizedIdx = ((vec4(pos,0) - cloudProperties.bounds[0])/(cloudProperties.bounds[1] - cloudProperties.bounds[0])).xyz;
    return texture(cloudSampler, normalizedIdx).x;
}

float sampleShadowVolume(in vec3 pos)
{
    vec3 normalizedIdx =
        (shadowVolumeProperties.basisChange * (vec4(pos, 1) - shadowVolumeProperties.bounds[0]) /
        (shadowVolumeProperties.basisChange * (shadowVolumeProperties.bounds[1] - shadowVolumeProperties.bounds[0]))).xyz;
    return texture(shadowVolumeSampler, normalizedIdx).x;
}

float average(in vec4 data)
{
    float accumulated = 0.0f;
    for(int i = 0; i < 4; i++)
    {
        accumulated += data[i];
    }
    return accumulated/4.0f;
}

int isNegativeSign(in float value)
{
    return int(value < 0);
}

void getCameraRay(in ivec2 coord, out Ray ray)
{
    ray.dir = cameraProperties.forward * cameraProperties.nearPlane
                    + cameraProperties.right  * cameraProperties.pixelSizeX * (coord.x - cameraProperties.halfWidth)
                    - cameraProperties.up * cameraProperties.pixelSizeY * (coord.y - cameraProperties.halfHeight);
    ray.pos = ray.dir + cameraProperties.position;
    ray.dir = normalize(ray.dir);
}

//https://www.scratchapixel.com/lessons/3d-basic-rendering/minimal-ray-tracer-rendering-simple-shapes/ray-box-intersection
bool intersectCloud(in Ray ray, out vec3 intersectionPoint)
{
    float tmin, tmax, tymin, tymax, tzmin, tzmax;
    vec3 invdir = 1 / ray.dir;
    int sign[3] = {isNegativeSign(invdir.x), isNegativeSign(invdir.y), isNegativeSign(invdir.z)};

    intersectionPoint = vec3(FLT_MAX);
 
    tmin = (cloudProperties.bounds[sign[0]].x - ray.pos.x) * invdir.x; 
    tmax = (cloudProperties.bounds[1-sign[0]].x - ray.pos.x) * invdir.x; 
    tymin = (cloudProperties.bounds[sign[1]].y - ray.pos.y) * invdir.y; 
    tymax = (cloudProperties.bounds[1-sign[1]].y - ray.pos.y) * invdir.y; 
 
    if ((tmin > tymax) || (tymin > tmax)) 
        return false; 
    if (tymin > tmin) 
        tmin = tymin; 
    if (tymax < tmax) 
        tmax = tymax; 
 
    tzmin = (cloudProperties.bounds[sign[2]].z - ray.pos.z) * invdir.z; 
    tzmax = (cloudProperties.bounds[1-sign[2]].z - ray.pos.z) * invdir.z; 
 
    if ((tmin > tzmax) || (tzmin > tmax)) 
        return false; 
    if (tzmin > tmin) 
        tmin = tzmin; 
    if (tzmax < tmax) 
        tmax = tzmax;

    // We just want intersections in front of the ray
    if(tmax < 0) // ray outside of the cloud, pointing away
    {
        return false;
    }
    else if(tmin < 0) // ray is inside the cloud
    {
        intersectionPoint = ray.pos + ray.dir * tmax;
    }
    else  // ray outside the cloud, pointing torwards it
    {
        intersectionPoint = ray.pos + ray.dir * tmin;
    }

    return true; 
}

bool isInCloud(in vec3 point)
{
    return !(point.x < cloudProperties.bounds[0].x || 
            point.y < cloudProperties.bounds[0].y || 
            point.z < cloudProperties.bounds[0].z || 
            point.x > cloudProperties.bounds[1].x || 
            point.y > cloudProperties.bounds[1].y || 
            point.z > cloudProperties.bounds[1].z );
}


vec3 dirFromPolar(float theta, float phi)
{
    return vec3(
        sin(theta) * cos(phi),
        sin(theta) * sin(phi),
        cos(theta));
}

void leaveCloud(in Ray ray, out vec3 intersectionPoint)
{
    float tmin, tmax, tymin, tymax, tzmin, tzmax;
    vec3 invdir = 1 / ray.dir;
    int sign[3] = {isNegativeSign(invdir.x), isNegativeSign(invdir.y), isNegativeSign(invdir.z)};

    intersectionPoint = vec3(FLT_MAX);
 
    tmin = (cloudProperties.bounds[sign[0]].x - ray.pos.x) * invdir.x; 
    tmax = (cloudProperties.bounds[1-sign[0]].x - ray.pos.x) * invdir.x; 
    tymin = (cloudProperties.bounds[sign[1]].y - ray.pos.y) * invdir.y; 
    tymax = (cloudProperties.bounds[1-sign[1]].y - ray.pos.y) * invdir.y; 
 
    if ((tmin > tymax) || (tymin > tmax)) 
        return; 
    if (tymin > tmin) 
        tmin = tymin; 
    if (tymax < tmax) 
        tmax = tymax; 
 
    tzmin = (cloudProperties.bounds[sign[2]].z - ray.pos.z) * invdir.z; 
    tzmax = (cloudProperties.bounds[1-sign[2]].z - ray.pos.z) * invdir.z; 
 
    //if ((tmin > tzmax) || (tzmin > tmax)) 
    //    return false; 
    if (tzmin > tmin) 
        tmin = tzmin; 
    if (tzmax < tmax) 
        tmax = tzmax;

	intersectionPoint = ray.pos + ray.dir * tmax;
}
//---------------------------------------------------------
// PNRG and Noise functions
//---------------------------------------------------------
uint rng_state = 0;
uint rand_xorshift()
{
	// Xorshift algorithm from George Marsaglia's paper
	rng_state ^= (rng_state << 13);
	rng_state ^= (rng_state >> 17);
	rng_state ^= (rng_state << 5);
	return rng_state;
}

float generateRandomNumber()
{
	return float(rand_xorshift()) / 4294967296.0f;
}

void initializeRandom(uint seed) {
	rng_state = seed;
	for (int i = 0; i < seed % 7 + 2; i++)
	 	generateRandomNumber();
}


//---------------------------------------------------------
// Phase BSDF - Henyey-Greenstein
//---------------------------------------------------------

float calculatePDF(float costheta)
{
    return INV_4Pi * parameters.phaseOneMinusG2 / pow(parameters.phaseOnePlusG2 - 2.0f * parameters.phaseG * costheta, 1.5f);
}
// Assumes non-isotropic due to division by phaseG
float invertCDF(float xi)
{
    float sqrTerm  = (parameters.phaseOneMinusG2) / (1.0f - parameters.phaseG + 2.0f * parameters.phaseG * xi);
    return parameters.phaseOneOver2G * (parameters.phaseOnePlusG2 - sqrTerm  * sqrTerm);
}

// Evaluate radiance
float samplePhase(in const vec3 incomingDirection, in const vec3 sampleDirection)
{
    float pdf;

    if(parameters.isotropic)
    {
        pdf = INV_4Pi;
    }
    else
    {
        float cosTheta = dot(incomingDirection, sampleDirection);
        pdf = calculatePDF(cosTheta);
    }

    return pdf;
}

//---------------------------------------------------------
// Photon map sampling with given ray pos and dor
//---------------------------------------------------------
vec4 samplePhotonMap(in const vec3 pos, in const vec3 dir)
{
    // Find all cells intersecting the cube radius
    vec3 gridPosition = pos - photonMapProperties.bounds[0].xyz;
    int xMin = max(int((gridPosition.x - photonMapProperties.sampleRadius) / photonMapProperties.cellSize), 0);
    int xMax = min(int((gridPosition.x + photonMapProperties.sampleRadius) / photonMapProperties.cellSize), int(photonMapProperties.cellCount.x - 1));
    int yMin = max(int((gridPosition.y - photonMapProperties.sampleRadius) / photonMapProperties.cellSize), 0);
    int yMax = min(int((gridPosition.y + photonMapProperties.sampleRadius) / photonMapProperties.cellSize), int(photonMapProperties.cellCount.y - 1));
    int zMin = max(int((gridPosition.z - photonMapProperties.sampleRadius) / photonMapProperties.cellSize), 0);
    int zMax = min(int((gridPosition.z + photonMapProperties.sampleRadius) / photonMapProperties.cellSize), int(photonMapProperties.cellCount.z - 1));

    vec3 photonDir;
    vec4 accumulatedFlux = vec4(0);
    uint currentIdx = 0;
    float sqrRadius = photonMapProperties.sampleRadius * photonMapProperties.sampleRadius;
    vec3 distVector;
    for(int z = zMin; z <= zMax; z++)
    {
        for(int y = yMin; y <= yMax; y++)
        {
            for(int x = xMin; x <= xMax; x++)
            {
                currentIdx = x + (y + z * photonMapProperties.cellCount.y) * photonMapProperties.cellCount.x;
                distVector = photons[currentIdx].position.xyz - pos;
                if(dot(distVector, distVector) < sqrRadius)
                {
                    photonDir = dirFromPolar(photons[currentIdx].theta, photons[currentIdx].phi);
                    accumulatedFlux += samplePhase(dir, photonDir) * photons[currentIdx].power * collisions[currentIdx].x; // Russian roulette scaling
                    //accumulatedFlux += photons[currentIdx].power * collisions[currentIdx].x; // Russian roulette scaling
                }
            }
        }
    }

    // Divide by sphere volume - 1/sigma_s is not considered because it is cancelled out with the rendering equation
    accumulatedFlux /= PI4_3 * photonMapProperties.sampleRadius * sqrRadius;

    return accumulatedFlux;
}

//---------------------------------------------------------
// Main
//---------------------------------------------------------
void main() 
{
    initializeRandom(pushConstants.seed * 1000000 + gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x);
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);

    // Get ray direction and volume entry point
    Ray ray;
    vec3 endPoint;
    getCameraRay(pixelCoord, ray);
    vec4 result = sampleBackground(ray.dir);
    if(intersectCloud(ray, endPoint))
    {
        // Ray marching loop
        vec3 point;
        leaveCloud(ray, ray.pos);
        float maxDistance = distance(ray.pos, point);
        float maxStep = photonMapProperties.stepSize;
        float currentDistance = 0;
        float attenuation = 0;

        vec4 directScatter;
        vec4 indirectScatter;
        vec4 radiance = vec4(0);
        vec3 currentPoint;
        float currentStep;
        float scatter;
        float extinction;
        while(currentDistance < maxDistance)
        {
            currentStep = photonMapProperties.stepSize + 10 *generateRandomNumber();
            currentPoint = ray.pos - ray.dir * (currentDistance + currentStep);
            extinction = sampleCloud(currentPoint) * cloudProperties.densityScaling / cloudProperties.baseScaling;
            scatter = (1 - photonMapProperties.absorption) * extinction;

            attenuation = exp( - extinction * currentStep );            
            indirectScatter = extinction > 0 ? samplePhotonMap(currentPoint, -ray.dir) / extinction : vec4(0);
            directScatter = samplePhase(-ray.dir, shadowVolumeProperties.lightDirection.xyz) * sampleShadowVolume(currentPoint) * SUNLIGHT_COLOR * parameters.lightIntensity;

            radiance = scatter * (indirectScatter + directScatter) + attenuation * result;

            result = radiance;
            currentDistance += currentStep;
        } 
    }

    // Accumulate result
    vec4 resultOld = imageLoad(resultImage, pixelCoord);
    result += resultOld * pushConstants.frameCount;
    result /= pushConstants.frameCount + 1;
    
	imageStore(resultImage, pixelCoord, result);
}