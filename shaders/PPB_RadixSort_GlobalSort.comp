#version 450

layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;
//---------------------------------------------------------
// Structs
//---------------------------------------------------------

struct PhotonBeam
{
    uint mortonCode;
	vec3 startPos;
    float radius;
    vec3 endPos;
	vec4 power;
};

//---------------------------------------------------------
// Consts
//---------------------------------------------------------
const uint MAX_UINT = 4294967295;
const uint ELEMENTS_PER_THREAD = 4;
const uint ELEMENTS_PER_WORK_GROUP = ELEMENTS_PER_THREAD * gl_WorkGroupSize.x;

//---------------------------------------------------------
// Descriptor Set
//---------------------------------------------------------
layout (binding = 0, std430) restrict buffer PhotonBeams
{
    uint beamCount;
    uint _padding_beams[3];
    PhotonBeam photonBeams[];
};

layout (binding = 1, std430) restrict buffer Histogram4b
{
    uint histogram[];
};

layout (binding = 2, std430) restrict buffer ScannedHistogram4b
{
    uint scannedHistogram[];
};

layout(push_constant) uniform PushConstants
{
    uint baseShift;
    uint currentBuffer;
    uint workGroupCount;
};

//---------------------------------------------------------
// Main
//---------------------------------------------------------
void main() 
{
    uint readBeamOffset;
    uint writeBeamOffset;
    if(currentBuffer != 0)
    {
        readBeamOffset = beamCount;
        writeBeamOffset = 0;
    }
    else
    {
        readBeamOffset = 0;
        writeBeamOffset = beamCount;
    }

    uint workGroupOffset = gl_WorkGroupID.x * ELEMENTS_PER_WORK_GROUP;
    uint startIdx = gl_LocalInvocationID.x * ELEMENTS_PER_THREAD;
    uint endIdx = startIdx + ELEMENTS_PER_THREAD;

    // Check if in bounds
    if(startIdx >= beamCount || startIdx + ELEMENTS_PER_THREAD >= beamCount)
    {
        return;
    }
    if(endIdx > beamCount)
    {
        endIdx = beamCount;
    }

    // Pre-calculate offsets
    uint offsets[ELEMENTS_PER_THREAD];
    uint mask = 15 << baseShift;
    uint maskedIdx = 0;
    uint bucketStartIdx = 0;
    for(uint i = 0; i < endIdx - startIdx; i++)
    {
        maskedIdx = photonBeams[readBeamOffset + workGroupOffset + startIdx + i].mortonCode & mask;
        maskedIdx = maskedIdx >> baseShift;
        offsets[i] = scannedHistogram[maskedIdx * gl_WorkGroupSize.x + gl_WorkGroupID.x];

        // Get offset from the start of the sequence with in the same key
        for(uint i = 0; i < maskedIdx; i++)
        {
            bucketStartIdx += histogram[gl_WorkGroupSize.x * i + gl_WorkGroupID.x];
        }
        offsets[i] += startIdx - bucketStartIdx;
    }

    // Copy locally sorted beams to their global positions
    for(uint i = 0; i < endIdx - startIdx; i++)
    {
        photonBeams[writeBeamOffset + offsets[i]] = photonBeams[readBeamOffset + workGroupOffset + i];
    }
}